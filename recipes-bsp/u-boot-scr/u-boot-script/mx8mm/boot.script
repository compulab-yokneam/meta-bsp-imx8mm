setenv nload ${loadaddr}
setenv oload ${fdt_addr}
setenv mmcdev 2
setenv mmcpart 1
setenv mmcoff 0x42
setenv bsize 0xB00
setenv bootloader ##BOOTLOADER##

# Load the new booloader from the file system
# 1) iface/dev/part defined by the default u-boot environment
# 2) the boot media second partition must have the device bootloader
# in the partition:boot directory
if load ${iface} ${dev}:2 ${nload} boot/${bootloader}
then

    # Read the current booloader from the boot device
    mmc dev ${mmcdev} ${mmcpart}
    mmc read ${oload} ${mmcoff} ${bsize}

    # Compare the old & the new one.
    if cmp.b ${nload} ${oload} ${filesize}
    then
        echo Nothing to update
    else
        echo Updating the bootloader on mmc [${mmcdev}:${mmcpart}]
        mmc write ${nload} ${mmcoff} ${bsize}
        mmc partconf ${mmcdev} 0 ${mmcpart} 0
        reset
    fi

else
    echo ##BOOTLOADER## file not found
fi

setenv script
boot
